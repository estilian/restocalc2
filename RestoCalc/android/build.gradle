// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
  ext.kotlin_version = '2.0.21'
  
  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:8.7.2'
    classpath 'com.google.gms:google-services:4.4.2'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

apply from: "variables.gradle"

ext {
  compileSdkVersion = 36
  targetSdkVersion  = 36
  minSdkVersion     = 24
}

allprojects {
  repositories {
    google()
    mavenCentral()
  }
}

subprojects { subproj ->
  afterEvaluate {
    def androidExt = subproj.extensions.findByName("android")
    if (androidExt != null) {
      androidExt.compileSdkVersion = rootProject.ext.compileSdkVersion
      def defCfg = androidExt.defaultConfig
      if (defCfg != null) {
        defCfg.targetSdkVersion = rootProject.ext.targetSdkVersion
        defCfg.minSdkVersion    = rootProject.ext.minSdkVersion
      }
      androidExt.compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
      }
      if (subproj.hasProperty("kotlinOptions")) {
        subproj.kotlinOptions { jvmTarget = "21" }
      }
    }
  }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}